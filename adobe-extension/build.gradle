apply plugin: 'com.android.library'

apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'com.verint.xm.sdk'

def extensionVersion = "2.0.0"
def sdkVersion = "7.0.4"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
    }

    buildTypes {
        all {
            buildConfigField "String", "AdobeExtensionVersion", "\"${extensionVersion}\""
        }
        debug {
            debuggable true
        }

        release {
            debuggable false
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.verint.xm.sdk'
            version "${extensionVersion}"

            // Set the artifactId
            artifactId 'adobeExtension'

            // Add the production release aar to the artifacts
            artifact("$buildDir/outputs/aar/${project.name}.aar")


            // Manually add dependencies to the pom.
            setupPomDependencies(pom)
        }
    }

    repositories {
        maven {
            mavenLocal()
        }
    }
}


def setupPomDependencies(pom) {
    pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        // Iterate over the compile dependencies.
        // These include api, releaseApi and implementation. But not compile!! Hence don't
        // use compile in projects.
        configurations.api.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
        configurations.releaseApi.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
        configurations.implementation.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
    }
}

static def pomConfig(it, dependenciesNode) {
    if (it.group != null && it.name != null) {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        if (it.name != 'unspecified') {
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Verint dependencies
    implementation "com.verint.xm.sdk:core:${sdkVersion}"
    implementation "com.verint.xm.sdk:predictive:${sdkVersion}"

    // Adobe dependencies
    compileOnly 'com.adobe.marketing.mobile:userprofile:1.1.0'
    compileOnly 'com.adobe.marketing.mobile:sdk-core:1.11.0'

    compileOnly 'com.android.support:appcompat-v7:26.1.0'
}
