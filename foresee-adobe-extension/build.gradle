apply plugin: 'com.android.library'

apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'com.foresee.sdk'

version = "2.0.0"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
    }

    buildTypes {
        all {
            buildConfigField "String", "AdobeExtensionVersion", "\"${version}\""
        }
        debug {
            debuggable true
        }

        release {
            debuggable false
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.foresee.sdk'
            version "${version}"

            // Set the artifactId
            artifactId 'adobeExtension'

            // Add the production release aar to the artifacts
            artifact("$buildDir/outputs/aar/${project.name}-${version}.aar")


            // Manually add dependencies to the pom.
            setupPomDependencies(pom)
        }
    }

    repositories {
        maven {
            mavenLocal()
        }
    }
}


def setupPomDependencies(pom) {
    pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        // Iterate over the compile dependencies.
        // These include api, releaseApi and implementation. But not compile!! Hence don't
        // use compile in projects.
        configurations.api.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
        configurations.releaseApi.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
        configurations.implementation.allDependencies.each {
            pomConfig(it, dependenciesNode)
        }
    }
}

static def pomConfig(it, dependenciesNode) {
    if (it.group != null && it.name != null) {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        if (it.name != 'unspecified') {
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Verint dependencies
    implementation 'com.foresee.sdk:core:7.0.0'
    implementation 'com.foresee.sdk:cxmeasure:7.0.0'


//    implementation fileTree(dir: '../libs', include: ['*.aar'])
//    implementation 'com.google.code.gson:gson:2.2.4'

    // Adobe dependencies
    compileOnly 'com.adobe.marketing.mobile:userprofile:1.1.0'
    compileOnly 'com.adobe.marketing.mobile:sdk-core:1.11.0'

    compileOnly 'com.android.support:appcompat-v7:26.1.0'
}
